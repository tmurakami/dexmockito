buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.github.ben-manes.versions'

group = 'com.github.tmurakami'
version = '0.5.1'

def packageName = "$group.$name" as String
def repackagedJarName = 'repackaged.jar'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.2'
    defaultConfig {
        minSdkVersion 5
        testInstrumentationRunnerArguments.package = packageName
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions.disable 'InvalidPackage'
    dexOptions.preDexLibraries !(System.getenv('CIRCLECI') as boolean)
}

repositories {
    jcenter()
}

configurations {
    jarjar
    repackage
}

dependencies {
    jarjar 'com.googlecode.jarjar:jarjar:1.3'
    repackage 'com.google.android.tools:dx:1.7'
    compile files("$buildDir/libs/$repackagedJarName") { builtBy 'jarjar' }
    compile 'org.mockito:mockito-core:2.1.0-RC.2'
    testCompile 'junit:junit:4.12'
}

task jarjar(type: Jar) {
    archiveName = repackagedJarName
    doLast {
        ant {
            taskdef name: 'jarjar',
                    classname: 'com.tonicsystems.jarjar.JarJarTask',
                    classpath: configurations.jarjar.asPath
            jarjar(jarfile: archivePath) {
                configurations.repackage.each { zipfileset src: it }
                keep pattern: 'com.android.dx.dex.cf.CfTranslator'
                rule pattern: 'com.android.dx.**', result: "${packageName}.repackaged.com.android.dx.@1"
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.resources.srcDirs
}

artifacts {
    archives sourcesJar
}
