buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.github.ben-manes.versions'

apply from: "$rootDir/gradle/version.gradle"

group = 'com.github.tmurakami'
version = version_name

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        minSdkVersion 5
        testInstrumentationRunnerArguments.package = "$group.$project.name" as String
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions.disable 'InvalidPackage'
    dexOptions.preDexLibraries !(System.getenv('CIRCLECI') as boolean)
}

repositories {
    jcenter()
}

dependencies {
    compile 'net.bytebuddy:byte-buddy-android:1.6.2'
    compile('org.mockito:mockito-core:2.5.6') { exclude module: 'byte-buddy-agent' }
    provided 'com.android.support.test:rules:0.5'
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.resources.srcDirs
}

artifacts {
    archives sourcesJar
}
