buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.github.ben-manes.versions'

group = 'com.github.tmurakami'
version = '0.5.0-SNAPSHOT'

def license = "$rootDir/LICENSE"
def licensesOutputDir = "$buildDir/intermediates/licenses"

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.3'
    defaultConfig {
        minSdkVersion 5
        testInstrumentationRunnerArguments.package = group
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    sourceSets.main.assets.srcDir licensesOutputDir
    lintOptions.disable 'InvalidPackage'
    dexOptions.preDexLibraries !(System.getenv('CIRCLECI') as boolean)
}

repositories {
    jcenter()
}

configurations {
    jarjar
}

dependencies {
    jarjar 'com.googlecode.jarjar:jarjar:1.3'
    compile 'org.mockito:mockito-core:2.1.0-RC.1'
    compile 'net.bytebuddy:byte-buddy:1.4.26'
    provided 'com.google.android.tools:dx:1.7'
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from license
    from android.sourceSets.main.java.sourceFiles
    from android.sourceSets.main.resources.sourceFiles
}

artifacts {
    archives sourcesJar
}

task copyLicenses(type: Copy) {
    from license
    from "$rootDir/dx-NOTICE"
    into licensesOutputDir
}

android.libraryVariants.all {
    def taskNameSuffix = name.capitalize()
    def jarTask = tasks["transformClassesAndResourcesWithSyncLibJarsFor$taskNameSuffix"]
    def inputDir = "$buildDir/intermediates/pre-jarjared/$dirName"
    def outputFile = jarTask.outputs.files.find { it.name == 'classes.jar' }
    jarTask.with {
        outputs.dir inputDir
        doLast {
            ant.move(file: outputFile, toDir: inputDir)
        }
    }
    tasks["bundle$taskNameSuffix"].dependsOn task("jarjar$taskNameSuffix", dependsOn: jarTask) {
        inputs.dir inputDir
        outputs.file outputFile
        doLast {
            ant {
                taskdef name: 'jarjar',
                        classname: 'com.tonicsystems.jarjar.JarJarTask',
                        classpath: configurations.jarjar.asPath
                jarjar(jarfile: outputFile) {
                    inputs.files.each { zipfileset src: it }
                    configurations.provided.each { zipfileset src: it }
                    keep pattern: "${applicationId}.**"
                    keep pattern: 'com.android.dx.dex.cf.CfTranslator'
                    rule pattern: 'com.android.dx.**', result: "${applicationId}.repackaged.com.android.dx.@1"
                }
            }
        }
    }
    mergeAssets.dependsOn(copyLicenses).inputs.dir licensesOutputDir
}
